/*
 * Facile JDBC Library (@__identifier__@).
 * Copyright (c) @__year__@ Franz Wilhelmstötter
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Author:
 *    Franz Wilhelmstötter (franz.wilhelmstoetter@gmail.com)
 */
import org.apache.tools.ant.filters.ReplaceTokens

/**
 * @author <a href="mailto:franz.wilhelmstoetter@gmail.com">Franz Wilhelmstötter</a>
 * @since 1.0
 * @version 1.0
 */

description = 'FacileJDBC Library'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	testCompile 'org.testng:testng:7.0.0'
	testCompile 'nl.jqno.equalsverifier:equalsverifier:3.1'
}

javadoc {
	project.configure(options) {
		memberLevel = 'PROTECTED'
		version = true
		author = true
		docEncoding = 'UTF-8'
		charSet = 'UTF-8'
		linkSource = true
		links = [
			'https://docs.oracle.com/en/java/javase/11/docs/api'
		]
		windowTitle = "FacileJDBC ${project.version}"
		docTitle = "<h1>FacileJDBC ${project.version}</h1>"
		bottom = "&copy; ${copyrightYear} Franz Wilhelmst&ouml;tter  &nbsp;<i>(${dateformat.format(now)})</i>"
		exclude '**/internal/**'

		options.tags = ["apiNote:a:API Note:",
						"implSpec:a:Implementation Requirements:",
						"implNote:a:Implementation Note:"]
	}
}

test {
	outputs.upToDateWhen { false }
	testLogging.showStandardStreams = true

	useTestNG {
		preserveOrder true
		parallel = 'tests' // 'methods'
		threadCount = Runtime.runtime.availableProcessors() + 1
	}
}

jar {
	manifest {
		attributes(
			'Implementation-Title': 'Facile JDBC library',
			'Implementation-Version': "${project.name}-${project.version}",
			'Implementation-URL': 'http://jenetics.io',
			'Implementation-Vendor': 'jenetics',
			'ProjectName': project.name,
			'Version': project.version,
			'Maintainer': 'Franz Wilhelmstötter',
			'Automatic-Module-Name': 'io.jenetics.facilejdbc'
		)
	}
}
